def includeCrashlytics() {
    return (project.hasProperty("crashlyticsdemoApisecret")  && project.hasProperty("crashlyticsdemoApikey"))
}


buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
            classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
if (includeCrashlytics()) {
    apply plugin: 'io.fabric'
}

repositories {
    maven { url "http://levelupstudio.com/maven2" }
    maven { url 'https://maven.fabric.io/public' }

}
repositories { mavenCentral() }

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.0 rc3"

    defaultConfig {
        applicationId "com.levelup.twitterforpalabre"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode Integer.parseInt(APP_VERSION_CODE)
        versionName APP_VERSION_NAME
    }
    buildTypes {
        debug {
            // disable crashlytics
                buildConfigField "boolean", "USE_CRASHLYTICS", "false"
                ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // enable crashlytics
            if (includeCrashlytics()) {
                buildConfigField "boolean", "USE_CRASHLYTICS", "true"
                ext.enableCrashlytics = true
            } else {
                buildConfigField "boolean", "USE_CRASHLYTICS", "false"
                ext.enableCrashlytics = false

            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'

    compile 'com.levelup.palabre.api:palabre-api:0.9.1'

    compile 'org.twitter4j:twitter4j-core:4.0.4'
        compile('com.crashlytics.sdk.android:crashlytics:2.5.0@aar') {
            transitive = true;
        }
}




afterEvaluate {
    if (includeCrashlytics()) {
        initCrashlyticsPropertiesIfNeeded()
    }
}

def initCrashlyticsPropertiesIfNeeded() {
    def propertiesFile = file('fabric.properties')
    if (!propertiesFile.exists()) {
        def commentMessage = "This is autogenerated crashlytics property from system environment to prevent key to be committed to source control."
        ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
            entry(key: "apiSecret", value: crashlyticsdemoApisecret)
            entry(key: "apiKey", value: crashlyticsdemoApikey)
        }
    }
}
